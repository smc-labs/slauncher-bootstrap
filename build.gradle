plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.4.6'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'application'
}

group 'ru.smclabs.bootstrap'
version '1.2.6'

mainClassName = project.group + '.BootstrapMain'
archivesBaseName = 'Bootstrap'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs "libs"
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    implementation 'ru.smclabs:report-api-1.0.0'
    compileOnly 'ru.smclabs:report-api-1.0.0-sources'

    implementation 'ru.smclabs:slauncher-http-1.0.3'
    compileOnly 'ru.smclabs:slauncher-http-1.0.3-sources'

    implementation 'ru.smclabs:slauncher-logger-1.0.0'
    compileOnly 'ru.smclabs:slauncher-logger-1.0.0-sources'

    implementation 'ru.smclabs:slauncher-models-1.0.2'
    compileOnly 'ru.smclabs:slauncher-models-1.0.2-sources'

    implementation 'ru.smclabs:slauncher-resources-1.0.7'
    compileOnly 'ru.smclabs:slauncher-resources-1.0.7-sources'

    implementation 'ru.smclabs:jackson-pack:1.0.0'
    implementation 'ru.smclabs:jackson-pack-config:1.0.0'

    implementation 'ru.smclabs:system-utils-1.0.1'
    compileOnly 'ru.smclabs:system-utils-1.0.1-sources'

    implementation 'ru.smclabs:system-utils-process-1.0.0'
    compileOnly 'ru.smclabs:system-utils-process-1.0.0-sources'

    implementation 'org.javassist:javassist:3.30.2-GA'
    implementation 'ru.smclabs:jSystemThemeDetector:3.8'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'org.slf4j:slf4j-nop:2.0.13'
    implementation 'com.github.oshi:oshi-core:6.6.1'
    implementation 'io.github.g00fy2:versioncompare:1.5.0'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    archiveBaseName.set('SimpleMinecraft')
    archiveClassifier.set('')
}

launch4j {
    version = project.version
    textVersion = project.version
    mainClassName = mainClassName
    icon = "${projectDir}/inno-setup/64.ico"
    jar = "${projectDir}/build/libs/SimpleMinecraft-${version}.jar"
    errTitle = 'Bootstrap'
    supportUrl = 'https://f.simpleminecraft.ru/'
    language = 'RUSSIAN'
    copyright = 'Created by SMC-Labs'
    fileDescription = archivesBaseName
    productName = archivesBaseName
    internalName = archivesBaseName
    outfile = archivesBaseName + '.exe'
    bundledJrePath = 'runtime/i586'
}