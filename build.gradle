plugins {
    id 'java'
    id 'application'
}

group 'ru.smclabs.bootstrap'
version '26.1.0'

application {
    mainClass = 'ru.smclabs.bootstrap.core.entrypoint.BootstrapMain'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "smcLabsReleases"
        url = uri("https://maven.simpleminecraft.ru/releases")
    }
    maven {
        name = "JitPack"
        url = uri("https://jitpack.io")
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2-1'

    // logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.apache.logging.log4j:log4j-core:2.25.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.3'

    // utils
    implementation 'ru.smclabs:report-api:1.1.0'
    implementation 'ru.smclabs.jackson:jackson-pack-config:1.2.0'
    implementation "ru.smclabs.system:system-core:2026.1.4"
    implementation 'ru.smclabs.slauncher.resources:resources-compressed:2026.2'
    implementation 'ru.smclabs.slauncher.resources:resources-downloader:2026.2'

    // theme detector
    implementation("com.github.oshi:oshi-core-java25:6.9.3")
    implementation("net.java.dev.jna:jna-platform-jpms:5.18.1")
    implementation("com.github.Dansoftowner:jSystemThemeDetector:3.9") {
        exclude group: "net.java.dev.jna"
        exclude group: "com.github.oshi", module: "oshi-core"
    }
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

// Убираем старый app-image, если он есть
tasks.register("appImageCleanup", Delete) {
    delete layout.buildDirectory.dir("app-image")
}

// Подготавливаем папку app-image: копируем JAR и зависимости
tasks.register("appImagePrepare") {
    dependsOn(tasks.named("jar"), tasks.named("appImageCleanup"))

    doLast {
        def outputDir = layout.buildDirectory.dir("app-image").get().asFile
        outputDir.mkdirs()

        // Копируем основной JAR
        def jarFile = tasks.named("jar").get().archiveFile.get().asFile
        copy {
            from jarFile
            into outputDir
        }

        // Копируем runtime зависимости
        configurations.runtimeClasspath.files.each { file ->
            copy {
                from file
                into outputDir
            }
        }
    }
}

// Строим app-image через jpackage
tasks.register("appImageBuild", Exec) {
    dependsOn(tasks.named("appImagePrepare"))

    doFirst {
        println("Starting jpackage build...")
        println("JDK: " + System.getProperty("java.vendor.version"))
    }

    doFirst {
        def outputDir = layout.buildDirectory.dir("app-image").get().asFile
        def jarFile = tasks.named("jar").get().archiveFile.get().asFile

        commandLine(
                "jpackage",
                "--type", "app-image",
                "--name", "bootstrap",
                "--app-version", project.version,
                "--vendor", "SMC Labs, Inc.",
                "--input", outputDir,
                "--main-jar", jarFile.name,
                "--main-class", application.mainClass.get(),
                "--icon", "inno-setup/favicon.ico",
                "--dest", outputDir
        )
    }

    doLast {
        println("App Image built successfully in ${layout.buildDirectory.dir("app-image").get().asFile}")
    }
}

tasks.register("packageDmg", Exec) {
    dependsOn(tasks.named("appImagePrepare"))

    doFirst {
        def outputDir = layout.buildDirectory.dir("installer").get().asFile
        def appImageDir = layout.buildDirectory.dir("app-image").get().asFile.absolutePath
        def jarFile = tasks.named("jar").get().archiveFile.get().asFile
        outputDir.mkdirs()

        commandLine(
                "jpackage",
                "--type", "dmg",
                "--name", "SimpleMinecraft",
                "--app-version", project.version,
                "--vendor", "SMC Labs, Inc.",
                "--input", appImageDir,
                "--main-jar", jarFile.name,
                "--main-class", application.mainClass.get(),
                "--dest", outputDir.absolutePath,
                "--icon", "icons/icon.icns"
        )
    }

    doLast {
        println("Removing quarantine for dev/debug...")
        project.exec {
            commandLine("xattr", "-dr", "com.apple.quarantine", new File(layout.buildDirectory.dir("installer").get().asFile, "bootstrap.app"))
        }
    }
}

