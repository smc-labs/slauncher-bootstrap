plugins {
    id 'java'
    id 'application'
}

group = 'ru.smclabs.bootstrap'
version = '26.1.4'

application {
    mainClass = 'ru.smclabs.bootstrap.core.BootstrapMain'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "smcLabsReleases"
        url = uri("https://maven.simpleminecraft.ru/releases")
    }
    maven {
        name = "JitPack"
        url = uri("https://jitpack.io")
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2-1'

    // logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.apache.logging.log4j:log4j-core:2.25.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.3'

    // utils
    implementation 'ru.smclabs:report-api:1.1.0'
    implementation 'ru.smclabs.jackson:jackson-pack-config:1.2.0'

    implementation "ru.smclabs.system:system-utils-core:26.1.1"
    implementation "ru.smclabs.system:system-utils-instance-locker:26.1.1"

    implementation 'ru.smclabs.slauncher.resources:resources-compressed:26.2.1'
    implementation 'ru.smclabs.slauncher.resources:resources-downloader:26.2.1'

    // theme detector
    implementation("com.github.oshi:oshi-core-java25:6.9.3")
    implementation("net.java.dev.jna:jna-platform-jpms:5.18.1")
    implementation("com.github.Dansoftowner:jSystemThemeDetector:3.9") {
        exclude group: "net.java.dev.jna"
        exclude group: "com.github.oshi", module: "oshi-core"
    }
}

def appImageRoot = layout.buildDirectory.dir("app-image")
def inputDirProvider = appImageRoot.map { it.dir("input") }
def outputDirProvider = appImageRoot.map { it.dir("build") }
def installerDirProvider = layout.buildDirectory.dir("installer")
def jarTask = tasks.named("jar", Jar)

tasks.register("appImageCleanup", Delete) {
    delete(appImageRoot)
}

tasks.register("appImagePrepare", Copy) {
    dependsOn("appImageCleanup")

    into(inputDirProvider)

    from(jarTask)
    from(configurations.runtimeClasspath)
}

tasks.register("appImageBuild", Exec) {
    dependsOn("appImagePrepare")

    def system = System.getProperty("os.name").toLowerCase(Locale.ROOT)

    def iconPath = system.contains("win") ? "windows/icon.ico"
            : system.contains("mac") ? "macos/icon.icns"
            : "linux/icon.png"

    doFirst {
        commandLine(
                "jpackage",
                "--type", "app-image",
                "--name", "bootstrap",
                "--app-version", project.version.toString(),
                "--vendor", "SMC Labs, Inc.",
                "--input", inputDirProvider.get().asFile.absolutePath,
                "--main-jar", jarTask.get().archiveFileName.get(),
                "--main-class", application.mainClass.get(),
                "--icon", file("installer/${iconPath}").absolutePath,
                "--dest", outputDirProvider.get().asFile.absolutePath
        )
    }
}

tasks.register("buildMacOsInstaller", Exec) {
    dependsOn(tasks.named("appImagePrepare"))

    def archProvider = providers.environmentVariable("ARCH")
            .orElse(providers.systemProperty("os.arch"))

    def nameProvider = archProvider.map { arch ->
        "simpleminecraft-installer-${project.version}-${arch}.dmg"
    }

    inputs.dir(inputDirProvider)
    outputs.file(installerDirProvider.map { it.file(nameProvider.get()) })

    commandLine(
            "jpackage",
            "--type", "dmg",
            "--name", "SimpleMinecraft",
            "--app-version", project.version,
            "--vendor", "SMC Labs, Inc.",
            "--input", inputDirProvider.get().asFile.absolutePath,
            "--main-jar", jarTask.get().archiveFileName.get(),
            "--main-class", application.mainClass.get(),
            "--dest", installerDirProvider.get().asFile.absolutePath,
            "--icon", file("installer/macos/icon.icns").absolutePath
    )

    doLast {
        def installerDir = installerDirProvider.get().asFile
        def original = new File(installerDir, "SimpleMinecraft-${project.version}.dmg")
        def target = new File(installerDir, nameProvider.get())

        if (original.exists()) {
            original.renameTo(target)
            println("MacOs installer built → ${target}")
        } else {
            println("MacOs installer not found in ${installerDir}")
        }
    }
}

tasks.register("buildWindowsInstaller", Exec) {
    dependsOn(tasks.named("appImageBuild"))

    def archProvider = providers.environmentVariable("ARCH")
            .orElse(providers.systemProperty("os.arch"))

    def normalizedArchProvider = archProvider.map { raw ->
        switch (raw.toLowerCase()) {
            case ["amd64", "x86_64", "x64"]:
                return "x64"
            case ["aarch64", "arm64"]:
                return "arm64"
            default:
                throw new GradleException("Unsupported architecture: $raw")
        }
    }

    def installerNameProvider = normalizedArchProvider.map { arch ->
        "simpleminecraft-installer-${project.version}-${arch}"
    }

    def resourcesDir = file("installer/windows/")
    def issFile = new File(resourcesDir, "installer.iss")

    inputs.dir(resourcesDir)
    outputs.file(installerDirProvider.map { it.file(installerNameProvider.get()) })

    doFirst {
        commandLine(
                "C:/Program Files (x86)/Inno Setup 6/ISCC.exe",
                issFile.absolutePath,
                "/O${installerDirProvider.get().asFile.absolutePath}",
                "/DArchitecture=${normalizedArchProvider.get()}",
                "/DAppVersion=${project.version}",
                "/DAppName=SimpleMinecraft",
                "/DAppPublisher=SMC Labs, Inc.",
                "/DAppPublisherURL=https://simpleminecraft.ru",
                "/DEntryPoint=bootstrap.exe",
                "/DOutputDir=${installerDirProvider.get().asFile.absolutePath}",
                "/DResourcesDir=${resourcesDir.absolutePath}",
                "/DBuiltImageDir=${outputDirProvider.map { it.dir("bootstrap") }.get().asFile.absolutePath}"
        )
    }

    doLast {
        println("Windows installer built → ${installerDirProvider.get().asFile}/${installerNameProvider.get()}")
    }
}

