plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.4.6'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'application'
}

group 'ru.smclabs.bootstrap'
version '2026.1'

mainClassName = project.group + '.BootstrapMain'
archivesBaseName = 'Bootstrap'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "smcLabsReleases"
        url = uri("https://maven.simpleminecraft.ru/releases")
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2-1'

    implementation 'ru.smclabs:report-api:[1.0,)'
    implementation 'ru.smclabs.system:system-process:[1.0.0,)'
    implementation 'ru.smclabs.jackson:jackson-pack-config:1.2.0'

    implementation 'ru.smclabs.slauncher.resources:resources-compressed:2026.1.2'
    implementation 'ru.smclabs.slauncher.resources:resources-downloader:2026.1.2'

    implementation 'org.javassist:javassist:3.30.2-GA'
    implementation 'ru.smclabs:jSystemThemeDetector:3.8'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'org.slf4j:slf4j-nop:2.0.13'
    implementation 'com.github.oshi:oshi-core:6.6.1'
    implementation 'io.github.g00fy2:versioncompare:1.5.0'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    archiveBaseName.set('SimpleMinecraft')
    archiveClassifier.set('')
}

launch4j {
    version = project.version
    textVersion = project.version
    mainClassName = mainClassName
    icon = "${projectDir}/inno-setup/64.ico"
    jar = "${projectDir}/build/libs/SimpleMinecraft-${version}.jar"
    errTitle = 'Bootstrap'
    supportUrl = 'https://f.simpleminecraft.ru/'
    language = 'RUSSIAN'
    copyright = 'Created by SMC-Labs'
    fileDescription = archivesBaseName
    productName = archivesBaseName
    internalName = archivesBaseName
    outfile = archivesBaseName + '.exe'
    bundledJrePath = 'runtime/i586'
}